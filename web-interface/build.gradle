buildscript {
    ext.karibudsl_version = '0.4.3'
    ext.vaadin10_version = '10.0.0.beta10'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "org.gretty" version "2.1.0"
    id "io.spring.dependency-management" version "1.0.4.RELEASE"  // remove when https://github.com/gradle/gradle/issues/4417 is fixed
}

group = 'org.fidonet.jftn.plugin'
description = ''

apply plugin: 'kotlin'
apply plugin: 'war'

gretty {
    contextPath = ''
}

dependencyManagement {
    imports { mavenBom "com.vaadin:vaadin-bom:$vaadin10_version" }
}

test {
    useJUnitPlatform()
    testLogging {
        // to see the exceptions of failed tests in Travis-CI console.
        exceptionFormat 'full'
    }
}

dependencies {
    // Karibu-DSL dependency, includes Vaadin
    compile("com.github.vok.karibudsl:karibu-dsl-v10:$karibudsl_version")
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // logging
    // currently we are logging through the SLF4J API to LogBack. See src/main/resources/logback.xml file for the logger configuration
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.41"

    // test support
    testCompile "com.github.kaributesting:karibu-testing-v10:0.4.14"
    testCompile "com.github.mvysny.dynatest:dynatest:0.8"

    // heroku app runner
    testRuntime 'com.github.jsimone:webapp-runner:8.5.30.0'

    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.10.v20160621'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly project(':plugin-manager')
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// Heroku
task stage(dependsOn: ['build'])
task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.testRuntime) {
        include "webapp-runner*"
    }
}

stage.dependsOn(copyToLib)
